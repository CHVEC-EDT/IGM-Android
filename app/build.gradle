apply plugin: 'com.android.application'

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

Process process = "git rev-list --all --count HEAD".execute()
process.waitFor()
int commitCount = process.getText() as int

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.bytemoe.parkingassistant"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode commitCount
        versionName "1.2.1.build-${versionCode}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("parkingassistant.jks")
            storePassword "111112113"
            keyAlias "parkingassistant"
            keyPassword "111112113"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    def fileName = "${defaultConfig.applicationId}-${defaultConfig.versionName}.${releaseTime()}.apk"
                    outputFileName = fileName
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation("com.squareup.okhttp3:okhttp:4.5.0")
    implementation("com.squareup.moshi:moshi:1.9.2")
    implementation 'com.google.android.material:material:1.1.0'
    api 'com.tonystark.android:socket:4.2.3'
}
